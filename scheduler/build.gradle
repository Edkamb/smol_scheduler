plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow'
}

group 'no.uio'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    // Do not change jena version
    implementation 'org.apache.jena:apache-jena-libs:3.16.0'
    implementation 'org.apache.jena:jena-core:3.16.0'
    implementation 'com.github.eduardz1:SemanticObjects:-SNAPSHOT'
    implementation group: 'com.jcraft', name: 'jsch', version: '0.1.54'
    implementation 'software.amazon.cryptools:AmazonCorrettoCryptoProvider:2.+:linux-x86_64'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveFileName='smol_scheduler.jar'
}
assemble.dependsOn shadowJar

jar {
    manifest {
        attributes(
                'Main-Class': 'Main'
        )
    }
}

task copyJar(type: Copy) {
    from shadowJar
    into 'demo/'
}

task demo(type: Exec) {
    dependsOn copyJar
    workingDir 'demo/'
    commandLine 'java', '-jar', 'smol_scheduler.jar'
}
